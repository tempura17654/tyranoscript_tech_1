; [set_default_view] 解説画面を作成する独自マクロです。(詳細は「macro.ks」をご参照ください)
[set_default_view]

; [akn/def] 独自マクロ。キャラ名欄に「あかね」と入れつつ、あかねの表情を変えます。定義は「macro.ks」で。
[akn/def]



; == 06_ptext.ks ===========================================
;
; 
;
; ★登場する主なタグ。
; [ptext] … 指定レイヤーに文字オブジェクトを出します。
;
; ==========================================================



;-----------------------------------------------------------
*Start
;-----------------------------------------------------------

画像の次は、文字!![p]
文字オブジェクトについて見ていこうね。[p]



;-----------------------------------------------------------
*Part1
;-----------------------------------------------------------

レイヤー上に文字オブジェクトを出すタグは［ptext］。[p]
さっそく使ってみよう。[p]

; [ptext] 文字オブジェクトを出します。
[ptext layer="1" x="0" y="0" size="40" text="文字オブジェクト～。"]

はいっ!![p]
前景レイヤー1番の左上に文字オブジェクトを出してみたよ。[p]
これは一番基本的な例です。[p]

; [freeimage] 前景レイヤー1番を解放(＝オブジェクトを全部削除)します。
[freeimage layer="1" time="700"]

いったん消して……と。[p]
さらに、できることをひとつひとつ見ていこう。[p]



;-----------------------------------------------------------
*Part2
;-----------------------------------------------------------

「文字の装飾」について。[p]
文字の大きさや色の変更、太字、縁取り、影付けなど……。[p]
［font］タグで指定できるものは［ptext］タグでもほとんど指定できるよ。[p]

; [ptext]x6 文字オブジェクトはいろいろ装飾できます。
[ptext layer="1" x="0" y="  0" text="(1)文字オブジェクト～。" size="40"]
[ptext layer="1" x="0" y=" 50" text="(2)文字オブジェクト～。" size="40"   bold="bold"    ]
[ptext layer="1" x="0" y="100" text="(3)文字オブジェクト～。" size="40"  color="0xff0000"]
[ptext layer="1" x="0" y="150" text="(4)文字オブジェクト～。" size="40"   edge="0xff0000"]
[ptext layer="1" x="0" y="200" text="(5)文字オブジェクト～。" size="40" shadow="0xff0000"]
[ptext layer="1" x="0" y="250" text="(6)文字オブジェクト～。" size="40"   face="Meiryo"  ]

実際にやってみると、ご覧の通り!![p]

; [freeimage] 前景レイヤー1番を解放します。
[freeimage layer="1" time="700"]



;-----------------------------------------------------------
*Part3
;-----------------------------------------------------------

「文字の方向」について。[p]
縦書きを指定することもできます。[p]

; [ptext]x2 縦書き、横書きでそれぞれ文字オブジェクトを出してみます。vertical="true"で縦書き。
[ptext layer="1" x="0" y="50" size="40" edge="0x000000" text="(縦)文字オブジェクト～。" vertical="true" ]
[ptext layer="1" x="0" y=" 0" size="40" edge="0x000000" text="(横)文字オブジェクト～。" vertical="false"]

この通り!![p]
[freeimage layer="1" time="700"]



;-----------------------------------------------------------
*Part4
;-----------------------------------------------------------

「文字の横方向の位置」について。[p]
文字を揃える方向を左・中央・右から選ぶことができるよ。[p]

; [ptext]x3 width(ウィズ)とalign(アライン)パラメータを両方指定することで、揃え方向を指定できます。
[ptext layer="2" x="0" y="  0" text="(左)文字オブジェクト～。" size="40" width="960" align="left"  ]
[ptext layer="2" x="0" y=" 50" text="(左)二つ目～。"           size="40" width="960" align="left"  ]
[ptext layer="2" x="0" y="100" text="(左)三つ目。"             size="40" width="960" align="left"  ]
[ptext layer="2" x="0" y="150" text="(中)文字オブジェクト～。" size="40" width="960" align="center"]
[ptext layer="2" x="0" y="200" text="(中)二つ目～。"           size="40" width="960" align="center"]
[ptext layer="2" x="0" y="250" text="(中)三つ目。"             size="40" width="960" align="center"]
[ptext layer="2" x="0" y="300" text="(右)文字オブジェクト～。" size="40" width="960" align="right" ]
[ptext layer="2" x="0" y="350" text="(右)二つ目～。"           size="40" width="960" align="right" ]
[ptext layer="2" x="0" y="400" text="(右)三つ目。"             size="40" width="960" align="right" ]

ほらっ。[p]
……見にくいかな。[p]
[image layer="1" x="  0" y="  0" storage="color/black.png"  width="960" height="460"]
[image layer="1" x="  0" y="  0" storage="color/red.png"    width=" 10" height="150"]
[image layer="1" x="470" y="155" storage="color/red.png"    width=" 10" height="150"]
[image layer="1" x="950" y="310" storage="color/red.png"    width=" 10" height="150"]
これでどうだー![p]
それぞれのまとまりで左、中央、右に揃っているのがわかるでしょ。[p]
これを使えば、たとえばキャラクターの名前欄を中央揃えにしたり……。[p]
何かの数値を見せるときに右揃えにしたり、といったことができるね。[p]
[freeimage layer="2" time="700" wait="false"]
[freeimage layer="1" time="700"]



;-----------------------------------------------------------
*Part5
;-----------------------------------------------------------

また［ptext］は「既存の文字オブジェクトの内容を書き換える」ということもできるの。[p]
実際にやってみよう。[p]

; [ptext] 文字オブジェクトを出す。ここではテキトーに、my_objectという名前を付けています。
[ptext layer="1" x="0" y="0" name="my_object" text="文字オブジェクト～。" size="40" edge="0x000000" width="960" align="right"]

右上に文字オブジェクトを出したよ。[p]
さらにこの内容を、[p]

; [ptext] 内容を上書き。先ほど付けていたmy_objectという名前を指定。
[ptext layer="1" x="0" y="0" name="my_object" text="上書きしてみる。" overwrite="true"]

; overwrite="true"を指定している場合、
; 同時に指定しているnameの文字オブジェクトの内容を上書きします。
; ちなみに、もし指定しているnameの文字オブジェクトが存在しなかった場合、
; 新規文字オブジェクトを追加する処理を行いますので、注意してください。

上書きしてみる。[p]
このように、位置や装飾設定そのままで内容を書き換えることができるよー!![p]
[freeimage layer="1" time="700"]



;-----------------------------------------------------------
*Part6
;-----------------------------------------------------------

いままで、文字オブジェクトは瞬間的に表示してきたけれど……。[p]
画像オブジェクト同様、時間をかけてゆっくり表示することもできるんだ。[p]

; [ptext][wait] 時間をかけて表示→その完了を待ちます。
[ptext     time="1400" layer="1" x="300" y="300" name="my_object" text="文字オブジェクト～。" size="40" edge="0x000000"]
[wait      time="1400"]

…………ね？[p][akn/hap]
以上、すべて覚えることができれば、あなたも立派な文字オブジェクトマスター!![p]



;-----------------------------------------------------------
*End
;-----------------------------------------------------------

; [jump] 目次画面にジャンプします。
[jump storage="select.ks"]