; [set_default_view] 解説画面を作成する独自マクロです。(詳細は「macro.ks」をご参照ください)
[set_default_view]

; [akn/def] 独自マクロ。キャラ名欄に「あかね」と入れつつ、あかねの表情を変えます。定義は「macro.ks」で。
[akn/def]



; == 01_text.ks ============================================
;
; テキストを出すところから学びます。
;
; ★登場する主なタグ。
; [l]   … クリックを待ちます。
; [r]   … 改行します。
; [er]  … メッセージをクリアします。
; [cm]  … メッセージをクリアします。※[er]との違いについては下記で触れています。
; [p]   … クリックを待ちつつ、クリックされたらメッセージをクリアします。
;
; ==========================================================



;-----------------------------------------------------------
*Start
;-----------------------------------------------------------

ティラノスクリプトでウィンドウにメッセージを出す方法は実に簡単！[l][er]
シナリオファイルにふつうに文字を打ち込んでいけば、こうしてテキストがウィンドウに出るよ。[l][er]

そしてふつうではない文字……すなわち「タグ」を使うことで、[l][er]
文章の途中で「クリックを待機」したり、[l][er]
文章の途中で[r]「改行」をしたり、[l][er]
文章の途中で[wait time="500"]「ウェイト」を挟んだり、[l][er]
ウィンドウ内のメッセージをクリアしたり[er]できるわけ。[l][er]

逆に「タグ」を使わないと、
ああああああああああああああああああああああ
ああああああああああああああああああああああ
……というように、ずーっとひたすら文章が出続けるので、注意してね。[l][er]

まずは基本的なタグを抑えよう！[l][er]
クリック待ちは［l］、メッセージクリアは［er］または［cm］、改行は［r］……。[l][er]
クリック待ち兼メッセージクリアは［p］で行うことができるよ。[p]

; --[er]と[cm]の違いについて。
; [er]が "現在操作中のメッセージレイヤーのメッセージをクリアする働き" を持つのに対し、
; [cm]は "すべてのメッセージレイヤーのメッセージをクリアする働き" を持ちます。
; それだけでなく[cm]は、"フリーレイヤー(ボタンオブジェクト等が配置されるレイヤー)を
; クリアする働き" もあります。
; [cm]のほうがいろんなものをクリアするということですね。
; 基本的には[cm]を使っておけば問題ないと思います。

[akn/hap]
とりあえずこれらのタグをどんどん使ってみて、メッセージを出すことに慣れていこう!![p]


;-----------------------------------------------------------
*End
;-----------------------------------------------------------

; [jump] 目次画面にジャンプします。
[jump storage="select.ks"]