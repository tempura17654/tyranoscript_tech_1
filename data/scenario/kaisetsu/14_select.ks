; [set_default_view] 解説画面を作成する独自マクロです。(詳細は「macro.ks」をご参照ください)
[set_default_view]

; [akn/def] 独自マクロ。キャラ名欄に「あかね」と入れつつ、あかねの表情を変えます。定義は「macro.ks」で。
[akn/def]



; == 14_select.ks ==========================================
;
; 選択肢のいろんな表現について。
;
; ★登場する主なタグ。
; [link]   … [endlink]とこれの間に囲まれたメッセージをリンク化します。
; [glink]  … 文字ボタンオブジェクトを出します。
; [button] … 画像ボタンオブジェクトを出します。

; ==========================================================



;-----------------------------------------------------------
*Start
;-----------------------------------------------------------

ノベルゲームといえば「選択肢」。[p][akn/hap]
選択肢によるシナリオの分岐はノベルゲームの醍醐味のひとつですな。[p][akn/def]
ティラノスクリプトは、いろんな選択肢の表現を開発者に提供します。[p]
では、順番に見せていこう。[p]



;-----------------------------------------------------------
*Part1
;-----------------------------------------------------------

昔のサウンドノベルではよく見かけた「リンクテキスト」。[p]
メッセージウィンドウ中のメッセージをクリックできるようにするやつだ。[p]
……朝食に食べるならどっち?![r]

; [link]～[endlink]x2 文字リンクを2個出します。ジャンプ先はどちらも*Part2。
[link target="*Part2"]【１】ごはん[endlink]
[link target="*Part2"]【２】パン[endlink]
[s]



;-----------------------------------------------------------
*Part2
;-----------------------------------------------------------

[er]リンクテキストに色を付けたりもできる。[p]
……即席麺ならどっち?![r]

; [font][link]～[endlink]x2 あらかじめ[font]を使っておくことで、色を付けたりできます。
[font color=red   edge=white][link target="*Part3"]【１】赤いうどん[endlink]
[font color=green edge=white][link target="*Part3"]【２】緑のそば[endlink]
[s]



;-----------------------------------------------------------
*Part3
;-----------------------------------------------------------

[er]文字オブジェクトによる選択肢。[p]
……午後のティータイムに飲みたいのは??

; [glink] 文字ボタンオブジェクトを出します。
[glink x="320" y="200" width="200" text="ミルクティー" target="*Part4" color="white"]
[glink x="320" y="300" width="200" text="レモンティー" target="*Part4" color="orange"]
[s]



;-----------------------------------------------------------
*Part4
;-----------------------------------------------------------

[cm]文字ボタンをメッセージレイヤーに乗っけてみたりして。[p]
……自分を虫に例えるならどっちだ!!

[glink x=" 50" y="560" width="300" text="キリギリス" target="*Part5" color="green" size=24]
[glink x="500" y="560" width="300" text="アリ"       target="*Part5" color="black" size=24]
[s]



;-----------------------------------------------------------
*Part5
;-----------------------------------------------------------

[cm]画像ボタンを選択肢に使う手もある。[p]
あなたの好みのタイプはどっちだ!!

; [button] 画像ボタンオブジェクトを出します。
[button graphic="../fgimage/chara/mascot_1/normal.png" enterimg="../fgimage/chara/mascot_1/happy.png" x=70 y=160  target="*Part6"]
[button graphic="../fgimage/chara/mascot_2/normal.png" enterimg="../fgimage/chara/mascot_2/happy.png" x=600 y=140 target="*Part6"]
[s]



;-----------------------------------------------------------
*Part6
;-----------------------------------------------------------

[cm]……いろいろあると思います。[p][akn/hap]
好みの表現を探してみよう!![p]



;-----------------------------------------------------------
*End
;-----------------------------------------------------------

; [jump] 目次画面にジャンプします。
[jump storage="select.ks"]