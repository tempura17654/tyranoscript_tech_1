; [set_default_view] 解説画面を作成する独自マクロです。(詳細は「macro.ks」をご参照ください)
[set_default_view]

; [akn/def] 独自マクロ。キャラ名欄に「あかね」と入れつつ、あかねの表情を変えます。定義は「macro.ks」で。
[akn/def]



; == 02_decotext.ks ========================================
;
; テキストの装飾表現について学びます。
;
; ★登場する主なタグ。
; [font]       … テキスト設定(フォント/色/大きさ/縁取り/影付け)を変更します。
; [deffont]    … デフォルトのテキスト設定を変更します。項目は上に同じ。
; [resetfont]  … テキスト設定をデフォルトに戻します。
; [ruby]       … ルビを挿入します。
; [graph]      … インライン画像を挿入します。
;
; ==========================================================



;-----------------------------------------------------------
*Start
;-----------------------------------------------------------

この節では、メッセージの装飾について扱うよ。[p]



;-----------------------------------------------------------
*Part1
;-----------------------------------------------------------

; ★
; ★一時的な装飾設定を変更する。
; ★

メッセージを一時的に装飾したいときにはね……［font］タグを使うの。[p]
そうすれば、

; [font]x7 フォントの設定をいろいろ変えます。
[font bold="true"                  ]字を太くしてみたり、[l]
[font bold="false" color="0x00FFFF"]色を変えてみたり、[l]
[font color="0xFFFFFF" size="25"   ]大きさを変えてみたり、[l]
[font face="Meiryo" size="40"      ]フォントを変えてみたり。[p]

[font size="40" edge="none" face="ロゴたいぷゴシック"]また最新のティラノスクリプトなら、
[font edge="0xFF0000" shadow="none"]文字を縁取ったり、[l]
[font edge="none" shadow="0xFF0000"]文字に影をつけたりもできるのだ!![p]



;-----------------------------------------------------------
*Part2
;-----------------------------------------------------------

; ★
; ★デフォルトの装飾設定を変更・反映する。
; ★

ちょっと見づらいかな？[p]
さっきまでの装飾に戻そうか。[p]
もともとの装飾設定に戻したいよ～ってときは、［resetfont］タグを使うか……。[p]
あるいは［er］タグや［cm］タグを使うことでも元通りになるよ。[p]

; [resetfont] デフォルトのフォント設定を反映します。
[resetfont]

ね？[p]
そして、「もともとの装飾設定」そのものを変えちゃうこともできる。[p]
一時的な装飾ではなくて、ゲーム中ずーっとその装飾で行きたい場合の話ね。[p]
そういうときは［font］タグではなくて、［deffont］タグを使うの。[p]

; [deffont][resetfont] デフォルトのフォント設定を変えて、反映します。
[deffont size="30"]
[resetfont]

このタグでもともとの設定を変更してから、それを［resetfont］で反映させると……。[p]
指定した装飾がずっと続くことになるよ。[p]



;-----------------------------------------------------------
*Part3
;-----------------------------------------------------------

; ★
; ★ルビについて。
; ★

それから、ルビについて。[p]
ティラノスクリプトでは、

; [ruby]x4 直後の1文字にルビを振ります。
[ruby text="も"]文
[ruby text="じ"]字に
[ruby text="す"]好きなルビを
[ruby text="ふ"]振ることができるよ。[p]

ただし、ルビは一文字単位で指定する必要があるので、そこは注意してね。[p]
それからフォントによっては行間に隙間ができることがあるので、そこも要注意です。[p]



;-----------------------------------------------------------
*Part4
;-----------------------------------------------------------

; ★
; ★表示速度について。
; ★

また、文字の表示速度を一時的に変えることも、

; [delay] 一文字あたりの文字表示速度を500ミリ秒(0.5秒)に変更します。
[delay speed="400"]

・・・・・・・・

; [nowait] 文字を一瞬で表示するように変更します。
[nowait]

ご覧の通り、できます。[p]

; [endnowait] nowaitを終了します。
[endnowait]

; [resetdelay] 文字表示速度をデフォルトに戻します。
[resetdelay]



;-----------------------------------------------------------
*Part5
;-----------------------------------------------------------

; ★
; ★インライン画像の挿入について。
; ★

さて![p]
このように、文字だけでもいろんな表現ができるんだけど……。[p]
［graph］タグを使うことで、文章中に画像を挿入できるの。[p]
それを使えば、こんなふうに絵文字を挿入してみたり……。

; [graph] インライン画像を表示します。
[graph storage="02_decotext_A.png"]

[p]「

; [graph]x3 インライン画像を表示します。
[graph storage="02_decotext_B.png"]
[graph storage="02_decotext_C.png"]
[graph storage="02_decotext_D.png"]

」みたいな暗号めいた画像を挿入したりできる。[p][akn/hap]
覚えておくと、なにか使いどころがあるかも!?[p]



;-----------------------------------------------------------
*End
;-----------------------------------------------------------

; [jump] 目次画面にジャンプします。
[jump storage="select.ks"]